buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.1.4.RELEASE'
	}
}

configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails details ->
		if (details.requested.group == 'org.apache.tomcat.embed') {
//			details.useVersion '8.0.9' TODO throws Exception in thread "main" java.lang.NoSuchMethodError: org.apache.catalina.core.StandardContext.loadOnStartup([Lorg/apache/catalina/Container;)V
			// see https://github.com/spring-projects/spring-boot/issues/1148 for more
//			details.useVersion '8.0.8' // reportedly last 8.x beta version :-)
			details.useVersion '7.0.54' // currently tested on this version
		}
	}
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'idea'

archivesBaseName = 'respsec'

// http://docs.spring.io/spring-boot/docs/1.0.0.BUILD-SNAPSHOT/reference/html/build-tool-plugins-gradle-plugin.html
apply plugin: 'spring-boot'
//jar.enabled = false // not sure how or when this is important
bootRepackage.enabled = false

repositories {
	mavenCentral()
}

dependencies {
	versionManagement 'io.spring.platform:platform-versions:1.0.0.RELEASE@properties'

	providedCompile 'javax:javaee-api:7.0'
	providedCompile 'net.sourceforge.jtds:jtds:1.3.1'
	compile 'org.springframework:spring-beans'
	compile 'org.springframework:spring-jdbc'
	compile 'org.springframework.security:spring-security-web'
	compile 'org.springframework.security:spring-security-config'
	compile 'org.springframework.security:spring-security-ldap'
	compile 'com.google.guava:guava'
	compile 'com.github.dblock.waffle:waffle-spring-security3:1.6'

	compile "org.springframework.boot:spring-boot-starter-web"
	testCompile "org.springframework.boot:spring-boot-starter-test"
}

task deployToLocalWildFly(type: Copy) {
	it.dependsOn war

	from 'build/libs'
	into 'c:/work/appservers/wildfly-8.1.0.Final/standalone/deployments/'
}

task deployToTomcat7(type: Copy) {
	it.dependsOn war

	from 'build/libs'
	into 'c:/work/appservers/apache-tomcat-7.0.54/webapps/'
}
